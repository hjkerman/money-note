import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val keystoreProperties = Properties()
val keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.inputStream().use { keystoreProperties.load(it) }
}

fun signingSecret(propertyName: String, environmentName: String): String? {
    return (keystoreProperties[propertyName] as String?)
        ?.takeIf { it.isNotBlank() }
        ?: System.getenv(environmentName)?.takeIf { it.isNotBlank() }
}

val releaseStoreFile = signingSecret("storeFile", "MONEY_NOTE_KEYSTORE_PATH")
val hasReleaseSigning = listOf(
    releaseStoreFile,
    signingSecret("storePassword", "MONEY_NOTE_KEYSTORE_PASSWORD"),
    signingSecret("keyAlias", "MONEY_NOTE_KEY_ALIAS"),
    signingSecret("keyPassword", "MONEY_NOTE_KEY_PASSWORD"),
).all { it != null }

android {
    namespace = "com.example.money_note_mobile"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "kr.re.hjkerman.money_note"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        if (hasReleaseSigning) {
            create("release") {
                storeFile = file(releaseStoreFile!!)
                storePassword = signingSecret("storePassword", "MONEY_NOTE_KEYSTORE_PASSWORD")
                keyAlias = signingSecret("keyAlias", "MONEY_NOTE_KEY_ALIAS")
                keyPassword = signingSecret("keyPassword", "MONEY_NOTE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            signingConfig = if (hasReleaseSigning) {
                signingConfigs.getByName("release")
            } else {
                signingConfigs.getByName("debug")
            }
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
